config { 
  type: 'table',
  tags: ["daily"],
  schema: "dataform_imdb_reporting",
  assertions: {
    uniqueKey: ["id", "title"],
    rowConditions: ["rating >= 0"]
  }
}

js {
  var currentDateTime = new Date().toISOString();
}

select 
  tb.tconst as id,
  tb.primary_title as title,
  tb.start_year as release_year,
  array(
    select 
      struct(d.director_id, nb.primary_name)
    from
      ${ ref('directors')} as d 
    inner join 
      ${ ref('name_basics')} as nb 
    on nb.nconst = d.director_id
    where d.movie_id = tb.tconst
  ) as directors,
  array(
    select
      struct(a.actor_id, nb.primary_name)
    from 
      ${ ref('actors')} as a 
    inner join 
      ${ ref('name_basics')} as nb 
    on nb.nconst = a.actor_id
    where a.movie_id = tb.tconst
  ) as actors,
  tr.num_votes as votes,
  tr.average_rating as rating,
  -- 자바스크립트 함수 적용 케이스
  ${convert_rating_to_stars.convertRatingToStars("tr.average_rating")} as rating_stars,
  -- 자바스크립트 변수 적용 케이스
  "${get_random_string.randomString}" as ingestion_id,
  -- 자바스크립트 변수 적용 케이스
  "${currentDateTime}" as ingestion_datetime
from
  ${ ref('title_basics_movie' )} as tb
left outer join 
  ${ ref('title_ratings')} as tr
  on tr.tconst = tb.tconst
where 
  tr.average_rating is not null

